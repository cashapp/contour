apply plugin: 'com.github.ben-manes.versions'

buildscript {
  ext.versions = [
    'minSdk': 21,
    'targetSdk': 30,
    'agp': '4.1.2',
    'lint': '27.1.2',  // = 23.0.0 + agp
    'kotlin': '1.5.0-RC',
  ]

  ext.deps = [
    'kotlin': [
      'stdlib': [
          'jdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",
      ],
    ],
    'androidx': [
      'appCompat': 'androidx.appcompat:appcompat:1.2.0',
      'ktx': 'androidx.core:core-ktx:1.3.2',
      'transition': 'androidx.transition:transition:1.4.0',
    ],
    'picasso': 'com.squareup.picasso:picasso:2.71828',
    'tools': [
      'lint': [
        'api': "com.android.tools.lint:lint-api:${versions.lint}",
        'checks': "com.android.tools.lint:lint-checks:${versions.lint}",
        'lint': "com.android.tools.lint:lint:${versions.lint}",
        'tests': "com.android.tools.lint:lint-tests:${versions.lint}",
      ]
    ],
    'robolectric': 'org.robolectric:robolectric:4.5.1',
    'truth': 'com.google.truth:truth:1.1.2',
    'plugins': [
      'agp': "com.android.tools.build:gradle:${versions.agp}",
      'kotlin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    ],
  ]

  dependencies {
    classpath deps.plugins.agp
    classpath deps.plugins.kotlin
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.36.0'
  }

  repositories {
    google()
    mavenCentral()
    jcenter()
  }
}

subprojects {
  repositories {
    google()
    mavenCentral()
    jcenter()
  }

  tasks.withType(JavaCompile).configureEach { task ->
    task.sourceCompatibility = JavaVersion.VERSION_1_8
    task.targetCompatibility = JavaVersion.VERSION_1_8
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
    task.kotlinOptions {
      jvmTarget = "1.8"
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

subprojects { project ->
  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }
}

tasks.wrapper {
  distributionType = Wrapper.DistributionType.BIN
}